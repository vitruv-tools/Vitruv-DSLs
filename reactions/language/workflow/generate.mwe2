module tools.vitruv.dsls.reactions

import org.eclipse.emf.mwe2.ecore.EcoreGenerator
import org.eclipse.emf.mwe.utils.StandaloneSetup

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.xtext.xtext.generator.textmate.*

var workspaceRoot = ".."

Workflow {

    bean = StandaloneSetup {
        scanClassPath = true
        platformUri = workspaceRoot
    }

    component = EcoreGenerator {
        genModel = "platform:/resource/tools.vitruv.dsls.reactions/src/main/ecore/InputTypes.genmodel"
        srcPath = "platform:/resource/tools.vitruv.dsls.reactions/target/generated-sources/ecore"
        generateCustomClasses = false
    }

    component = EcoreGenerator {
        genModel = "platform:/resource/tools.vitruv.dsls.reactions/src/main/ecore/TopLevelElements.genmodel"
        srcPath = "platform:/resource/tools.vitruv.dsls.reactions/target/generated-sources/ecore"
        generateCustomClasses = false
    }

    component = XtextGenerator {
        configuration = {
            project = StandardProjectConfig {
                baseName = "."
                rootPath = workspaceRoot
                mavenLayout = true

                runtime = {
                    root = workspaceRoot
                    ecoreModel = "${workspaceRoot}/target/generated-sources/xtext-ecore"
                    src = "${workspaceRoot}/src/main/xtend"
                    srcGen = "${workspaceRoot}/target/generated-sources/xtext-java"
                }
                runtimeTest = {
                    enabled = true
                    root = workspaceRoot
                    src = "${workspaceRoot}/src/test/xtend"
                    srcGen = "${workspaceRoot}/target/generated-test-sources/xtext-java"
                }
                genericIde = {
                    enabled = true
                    name = "../ide"
                    src = "${workspaceRoot}/../ide/src/main/xtend"
                    srcGen = "${workspaceRoot}/../ide/src-gen/generated-sources/xtext-java"
                }
            }
            code = {
                preferXtendStubs = true
                encoding = "UTF-8"
                fileHeader = "/*\n * generated by Xtext \${version}\n */"
            }
        }
        language = StandardLanguage {
            name = "tools.vitruv.dsls.reactions.ReactionsLanguage"
            fileExtensions = "reactions"
            grammarUri = "platform:/resource/tools.vitruv.dsls.reactions/src/main/xtext/tools/vitruv/dsls/reactions/ReactionsLanguage.xtext"
            referencedResource = "platform:/resource/tools.vitruv.dsls.common/src/main/ecore/CommonLanguageElements.genmodel"
            referencedResource = "platform:/resource/tools.vitruv.dsls.reactions/src/main/ecore/TopLevelElements.genmodel"

            formatter = {
                generateStub = true
            }

            fragment = TextMateHighlightingFragment {
            	absolutePath = "${workspaceRoot}/../ide/target"
            }

	        parserGenerator = {
			}

			serializer = {
            	generateStub = true
            }
            generator = {
                generateStub = true
            }
            scopeProvider = {
                generateStub = true
            }
            validator = {
                generateStub = true
            }

            junitSupport = junit.JUnitFragment {
                junitVersion = "5"
                generateStub = false
            }
        }
    }

}